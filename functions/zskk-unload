#autoload

# zskk-unload -- revert widget bindings and reset globals.
function zskk-unload {
  emulate -L zsh -o extended_glob

  if [[ -z ${_ZSKK_INITIALIZED:-} ]]; then
    return 0
  fi

  if (( ${#ZSKK_BOUND_WIDGETS[@]} )); then
    local entry kind raw keymap key original widget alias
    for entry in ${ZSKK_BOUND_WIDGETS[@]}; do
      kind=${entry%%:*}
      raw=${entry#*:}
      case ${kind} in
        override)
          widget=${raw%%:*}
          alias=${raw##*:}
          if zle -l | grep -Fxq -- "${alias}"; then
            zle -A "${alias}" "${widget}"
            zle -D "${alias}"
          fi
          ;;
        custom)
          alias=${raw}
          if zle -l | grep -Fxq -- "${alias}"; then
            zle -D "${alias}"
          fi
          ;;
        binding)
          keymap=${raw%%:*}
          raw=${raw#*:}
          key=${raw%%:*}
          original=${raw##*:}
          if [[ ${original} == __zskk-unbound__ ]]; then
            bindkey -M "${keymap}" -r "${key}"
          else
            bindkey -M "${keymap}" "${key}" "${original}"
          fi
          ;;
      esac
    done
  fi

  unset ZSKK_BOUND_WIDGETS
  unset ZSKK_STATE
  unset ZSKK_CACHE
  unset ZSKK_CONFIG
  unset ZSKK_ROMA_TABLE ZSKK_ROMA_PREFIX ZSKK_KATA_MAP
  unset ZSKK_WIDGET_ORIGINAL ZSKK_WIDGET_METADATA ZSKK_WIDGET_LAST_MESSAGE
  unset _ZSKK_ROMA_READY _ZSKK_ROMA_MAX_KEY_LENGTH REPLY2
  unset _ZSKK_INITIALIZED
}
